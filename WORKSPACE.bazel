workspace(name = "blah")

load("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

_sdl2_build_file = """
load("@rules_cc//cc:defs.bzl", "cc_import", "cc_library")
exports_files(glob(["**/*.dll"]))
cc_import(
    name = "SDL2_private",
    includes = ["include"],
    hdrs = glob(["include/*.h"], exclude = ["include/SDL_main.h"]),
    shared_library = "lib/x64/SDL2.dll",
    interface_library = "lib/x64/SDL2.lib",
)
cc_import(
    name = "SDL2main_private",
    includes = ["include"],
    hdrs = ["include/SDL_main.h"],
    shared_library = "lib/x64/SDL2.dll",
    interface_library = "lib/x64/SDL2main.lib",
    linkopts = ["-DEFAULTLIB:shell32.lib"],
)
cc_library(
    name = "SDL2",
    visibility = ["//visibility:public"],
    includes = ["include"],
    hdrs = glob(["include/*.h"], exclude = ["include/SDL_main.h"]),
    strip_include_prefix = "include",
    deps = [":SDL2_private"],
)
cc_library(
    name = "SDL2main",
    visibility = ["//visibility:public"],
    includes = ["include"],
    hdrs = ["include/SDL_main.h"],
    strip_include_prefix = "include",
    deps = [":SDL2main_private"],
)
"""

http_archive(
    name = "SDL2",
    strip_prefix = "SDL2-2.24.2",
    url = "https://github.com/libsdl-org/SDL/releases/download/release-2.24.2/SDL2-devel-2.24.2-VC.zip",
    sha256 = "fae7f8f1661fc1fb3ece7b58eca3b2aaeb2ed000a0b7766f859ae12511efd11f",
    build_file_content = _sdl2_build_file,
)
